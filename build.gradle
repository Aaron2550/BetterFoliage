buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}
apply plugin: 'forge'

minecraft {
	version = '1.7.10-10.13.0.1180'
}

group = 'com.github.octarine-noise'
version = "1.0.${System.getenv().BUILD_NUMBER}"

targetCompatibility = 1.7
sourceCompatibility = 1.7

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version
	
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

jar {
	manifest {
		attributes "FMLCorePlugin": "mods.betterfoliage.loader.BetterFoliageLoader"
        attributes "FMLCorePluginContainsFMLMod": "mods.betterfoliage.BetterFoliage"
	}
}

task srcJar(type: Jar, dependsOn: classes) {
    classifier = 'src'
    from sourceSets.main.allSource
    manifest {
        attributes "FMLCorePlugin": "mods.betterfoliage.loader.BetterFoliageLoader"
        attributes "FMLCorePluginContainsFMLMod": "mods.betterfoliage.BetterFoliage"
    }
}

task devJar(type: Jar, dependsOn: classes) {
    classifier = 'dev'
    from sourceSets.main.output
    manifest {
        attributes "FMLCorePlugin": "mods.betterfoliage.loader.BetterFoliageLoader"
        attributes "FMLCorePluginContainsFMLMod": "mods.betterfoliage.BetterFoliage"
    }
}

artifacts {
    archives jar
    archives srcJar
    archives devJar
}
