//
// Define MC and Kotlin versions to use, apply plugins
//
buildscript {
    ext.kotlin_version = '1.0.1-2'
    repositories {
        mavenCentral()
        maven { name = 'forge'; url = 'http://files.minecraftforge.net/maven' }
        maven { name = "sonatype"; url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    apply plugin: "net.minecraftforge.gradle.forge"
    apply plugin: 'kotlin'

    minecraft {
        version = '1.9-12.16.0.1865-1.9'
        mappings = 'snapshot_20160406'
        runDir = 'run'
    }
}

def includeShadedDeps(cfg) {
    project.copySpec {
        cfg.each { dep ->
            from(project.zipTree(dep)) {
                exclude 'META-INF', 'META-INF/**'
            }
        }
    }
}

def transformResources(proj) {
    proj.copySpec {
        from(proj.sourceSets.main.resources) { exclude 'mcmod.info' }
        from(proj.sourceSets.main.resources) { include 'mcmod.info' expand 'version':proj.version, 'mcversion':proj.minecraft.version }
    }
}
