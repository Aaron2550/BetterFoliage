apply plugin: "forge"
apply plugin: "kotlin"

group = 'com.github.octarine-noise'
version = "2.0.15"
archivesBaseName = rootProject.name + '-MC1.7.10'

buildscript {
    ext.kotlin_version = '1.1.1'
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
configurations {
    shade
    compile.extendsFrom shade
}
dependencies {
    shade "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
minecraft {
    version = '1.7.10-10.13.4.1448-1.7.10'
    srgExtra "PK: kotlin mods/betterfoliage/kotlin"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    into "${buildDir}/classes/main"
}

jar {
    manifest {
        attributes "FMLCorePlugin": "mods.betterfoliage.loader.BetterFoliageLoader"
        attributes "FMLCorePluginContainsFMLMod": "mods.betterfoliage.BetterFoliageMod"
    }
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
    exclude "optifine"
}

repositories {
    mavenCentral()
}