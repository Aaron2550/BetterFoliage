buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true) {
            exclude group: 'trove', module: 'trove'
        }
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'kotlin'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = 'Curse'
        url = 'https://minecraft.curseforge.com/api/maven/'
    }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"
    implementation "kottle:Kottle:$kottle_version"
    implementation "org.spongepowered:mixin:0.8-SNAPSHOT"
}

mixin {
    add sourceSets.main, "betterfoliage.refmap.json"
}

minecraft {
    mappings channel: "$mcp_mappings_channel", version: "$mcp_mappings_version"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'CORE'
            property 'forge.logging.console.level', 'debug'
            mods {
                betterfoliage {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'CORE'
            property 'forge.logging.console.level', 'debug'
            mods {
                betterfoliage {
                    source sourceSets.main
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += ("-Xno-param-assertions")
        freeCompilerArgs += ("-Xno-call-assertions")
    }
}

jar {
    archiveName = "BetterFoliage-${version}-Forge-${mc_version}.jar"

    manifest {
        from "src/main/resources/META-INF/MANIFEST.MF"
        attributes "Implementation-Version": "${version}"
    }
}